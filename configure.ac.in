dnl vi:set et ai sw=2 sts=2 ts=2: */
dnl
dnl Copyright (c) 2007-2011 Jannis Pohlmann <jannis@expidus.org>
dnl Copyright (c) 2012-2020 The Expidus development team
dnl
dnl This library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU Library General Public
dnl License as published by the Free Software Foundation; either
dnl version 2 of the License, or (at your option) any later version.
dnl
dnl This library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU Library General Public License for more details.
dnl
dnl You should have received a copy of the GNU Library General
dnl Public License along with this library; if not, write to the
dnl Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
dnl Boston, MA 02110-1301, USA.

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([markon_verinfo], [0:0:0])
m4_define([markon_version_api_major], [1])
m4_define([markon_version_major], [1])
m4_define([markon_version_minor], [0])
m4_define([markon_version_micro], [0])
m4_define([markon_version_build], [@REVISION@])
m4_define([markon_version_tag], [git])
m4_define([markon_version], [markon_version_major().markon_version_minor().markon_version_micro()ifelse(markon_version_tag(), [git], [markon_version_tag()-markon_version_build()], [markon_version_tag()])])

dnl ********************************************
dnl *** Full debug support for GIT snapshots ***
dnl ********************************************
m4_define([markon_debug_default], [ifelse(markon_version_tag(), [git], [yes], [minimum])])

dnl ****************************
dnl *** For intltool support ***
dnl ****************************
m4_define([intltool_minimum_version], [0.51])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2007-2011 Jannis Pohlmann <jannis@expidus.org>
Copyright (c) 2012-2020 The ExpidusOS and XFCE development team
              ])
AC_INIT([markon], [markon_version], [https://gitlab.expidus.org/expidus/markon])
AC_PREREQ([2.69])
AC_CONFIG_MACRO_DIRS([m4])
AC_REVISION([@REVISION@])
AC_CANONICAL_TARGET()

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.15 dist-bzip2 tar-ustar no-dist-gzip foreign])
AC_CONFIG_HEADERS([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl **************************
dnl *** Libtool versioning ***
dnl **************************
MARKON_VERINFO=markon_verinfo()
AC_SUBST([MARKON_VERINFO])

dnl ****************************
dnl *** Subst markon version ***
dnl ****************************
MARKON_VERSION_MAJOR=markon_version_major()
MARKON_VERSION_MINOR=markon_version_minor()
MARKON_VERSION_MICRO=markon_version_micro()
MARKON_VERSION_API_MAJOR=markon_version_api_major()
MARKON_VERSION_API=$MARKON_VERSION_API_MAJOR
AC_SUBST([MARKON_VERSION_MAJOR])
AC_SUBST([MARKON_VERSION_MINOR])
AC_SUBST([MARKON_VERSION_MICRO])
AC_SUBST([MARKON_VERSION_API_MAJOR])
AC_SUBST([MARKON_VERSION_API])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AM_PROG_AS()
AC_PROG_CC()
AC_PROG_INSTALL()
IT_PROG_INTLTOOL([intltool_minimum_version])
AM_PROG_CC_C_O()

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl ***************************************
dnl *** Check for standard header files ***
dnl ***************************************
AC_HEADER_STDC()
AC_CHECK_HEADERS([fcntl.h errno.h sys/mman.h sys/stat.h sys/wait.h memory.h \
                  stdlib.h stdio.h string.h sys/types.h sys/time.h unistd.h \
                  time.h stdarg.h sys/types.h sys/uio.h sched.h ctype.h])

dnl ************************************
dnl *** Check for standard functions ***
dnl ************************************
AC_FUNC_MMAP()
dnl AC_CHECK_FUNCS([])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.50.0])
XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.50.0])
XDT_CHECK_PACKAGE([LIBEXPIDUS1UTIL], [libexpidus1util-1.0], [1.0.0])
XDT_CHECK_PACKAGE([GOBJECT], [gobject-2.0], [2.50.0])
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.50.0])
XDT_CHECK_PACKAGE([GTK3], [gtk+-3.0], [3.20.0])
XDT_CHECK_PACKAGE([LIBEXPIDUS1UI2], [libexpidus1ui-2], [1.0.0])

dnl *************************
dnl *** Check for gtk-doc ***
dnl *************************
GTK_DOC_CHECK([1.0])

dnl ***************************************
dnl *** Check for GObject Introspection ***
dnl ***************************************
GOBJECT_INTROSPECTION_CHECK([1.50.0])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG([markon_debug_default])

dnl **************************************
dnl *** Check for linker optimizations ***
dnl **************************************
XDT_FEATURE_LINKER_OPTS()

AC_CONFIG_FILES([
Makefile
data/Makefile
data/expidus/Makefile
docs/Makefile
docs/reference/Makefile
docs/reference/version.xml
markon/Makefile
markon/markon-1.pc
markon/markon-config.h
markon-gtk/Makefile
markon-gtk/markon-gtk3-1.pc
icons/Makefile
icons/32x32/Makefile
po/Makefile.in
tests/Makefile
])
AC_OUTPUT

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Debug Support:  $enable_debug"
echo "* GObject Introspection support: $enable_introspection"
echo
